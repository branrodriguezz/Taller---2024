{4- Tres robots floreros tienen 8 intentos en total para juntar todas las flores dentro del cuadrante comprendido entre las esquinas (40,40) y (60,60). Para ello, en cada intento un robot fiscalizador indicar치 a un robot aleatorio la esquina a la que debe dirigirse. El fiscalizador calcular치 esta esquina de manera aleatoria. Al completarse los 8 intentos, los robots floreros deber치n depositar todas las flores juntadas en la esquina (10,10), y el robot fiscalizador deber치 informar la cantidad total de flores juntadas por los robots. Los robots floreros inician en las esquinas (1,1), (2,1) y (3,1) respectivamente, y el fiscalizador en la (4,1).}

programa p5ej4
procesos
  
  proceso juntarFlores (ES f: numero)
  comenzar
    
    mientras (HayFlorEnLaEsquina)
      tomarFlor
      f:= f + 1
  fin
  
  proceso realizarTarea (E av: numero; E ca: numero; ES flores: numero; E avIni: numero; E caIni: numero)
  
  comenzar
    
    BloquearEsquina (av,ca)
    Pos (av,ca)
    juntarFlores (flores)
    Pos (avIni,caIni)
    LiberarEsquina (av,ca)
    
  fin
  
  proceso depositarFlores (E flores: numero; E avIni: numero; E caIni: numero)
  comenzar
    
    BloquearEsquina (10,10)
    Pos (10,10)
    repetir flores
      depositarFlor
    Pos (avIni, caIni)
    LiberarEsquina (10,10)
    
  fin
  
  
areas
  
  areaR1: AreaP (1,1,1,1)
  areaR2: AreaP (2,1,2,1)
  areaR3: AreaP (3,1,3,1)
  areaCuadrante: AreaPC (40,40,60,60)
  areaFiscalizador: AreaP (4,1,4,1)
  esq: AreaPC (10,10,10,10)
  
robots
  
  robot florero
  variables
  
    ok: boolean
    flores: numero
    av, ca: numero
    avIni, caIni: numero
    
  comenzar
  
    avIni:= PosAv
    caIni:= PosCa
    
    flores:= 0
    
    RecibirMensaje (ok, rf)
    
    mientras (ok)
      RecibirMensaje (av, rf)
      RecibirMensaje (ca, rf)
      realizarTarea (av, ca, flores, avIni, caIni)
      RecibirMensaje (ok, rf)
   
    depositarFlores (flores, avIni, caIni)
    EnviarMensaje (flores, rf)
    
  fin
  
  robot fiscalizador
  variables
    id: numero
    av, ca: numero
    canTotal, flores: numero
    
  comenzar
    
    canTotal:= 0
    
    repetir 8
    
      Random (id, 1,3)
      Random (av, 40,60)
      Random (ca, 40,60)
      
      si (id = 1)
        EnviarMensaje (V, r1)
        EnviarMensaje (av, r1)
        EnviarMensaje (ca, r1)
        
      sino
        
        si (id = 2)
          EnviarMensaje (V, r2)
          EnviarMensaje (av, r2)
          EnviarMensaje (ca, r2)
          
        sino
          EnviarMensaje (V, r3)
          EnviarMensaje (av, r3)
          EnviarMensaje (ca, r3)
   
   
    EnviarMensaje (F, r1)
    EnviarMensaje (F, r2)
    EnviarMensaje (F, r3)
    
    repetir 3
      RecibirMensaje (flores,*)
      canTotal:= canTotal + flores
    
    Informar ('La cantidad total de flores juntadas por los r es', canTotal)
   
  fin
  
variables
  
  r1: florero
  r2: florero
  r3: florero
  rf: fiscalizador
  
comenzar
  
  AsignarArea (r1, areaR1)
  AsignarArea (r1, areaCuadrante)
  AsignarArea (r1, esq)
  AsignarArea (r2, areaR2)
  AsignarArea (r2, areaCuadrante)
  AsignarArea (r2, esq)
  AsignarArea (r3, areaR3)
  AsignarArea (r3, areaCuadrante)
  AsignarArea (r3, esq)
  AsignarArea (rf, areaFiscalizador)
  
  Iniciar (rf, 4,1)
  Iniciar (r1, 1,1)
  Iniciar (r2, 2,1)
  Iniciar (r3, 3,1)
  
fin
