programa p5ej1
procesos 
  
  proceso mandarIds 
  comenzar
    
    EnviarMensaje (1, r1)
    EnviarMensaje (2, r2)
    EnviarMensaje (3, a1)
    EnviarMensaje (4, a2)
    
  fin
  
  proceso juntarFlores (ES f: numero)
  comenzar
  
    mientras (HayFlorEnLaEsquina)
      tomarFlor
      f:= f + 1
      
  fin
  
  proceso juntarPapeles (ES p: numero)
  comenzar
    
    mientras (HayPapelEnLaEsquina)
      tomarPapel
      p:= p + 1
      
  fin
 
areas
  
  areaR1tramo1: AreaP (2,1,2,97) {equipo rojo}
  areaR1tramo2: AreaP (2,100,2,100) {equipo rojo}
  
  areaR2tramo1: AreaP (3,1,3,97) {equipo rojo}
  areaR2tramo2: AreaP (3,100,3,100) {equipo rojo}
  
  areaA1tramo1: AreaP (1,98,1,98) {equipo azul}
  areaA1tramo2: AreaP (4,98,100,98) {equipo azul}
  
  areaA2tramo1: AreaP (1,99,1,99) {equipo azul}
  areaA2tramo2: AreaP (4,99,100,99) {equipo azul}
  
  esq1: AreaPC (2,98,2,98)
  esq2: AreaPC (2,99,2,99)
  esq3: AreaPC (3,98,3,98)
  esq4: AreaPC (3,99,3,99)
  
  areaJefe : AreaP (1,1,1,1) {jefe}
  
robots
  
  robot equipoRojo
  variables
    id: numero
    f: numero
    av: numero
  comenzar
    f:= 0
    RecibirMensaje (id, rf) {recibo si soy 1 o 2}
    av:= PosAv
    
    repetir 96
      juntarFlores (f)
      mover
    juntarFlores (f)
    
    BloquearEsquina (av,98)
    mover
    juntarFlores (f)
    BloquearEsquina (av,99)
    mover
    LiberarEsquina (av,98)
    juntarFlores (f)
    mover
    LiberarEsquina (av,99)
    juntarFlores (f)
      
    EnviarMensaje (id, rf)
    EnviarMensaje (f, rf)
        
  fin
  
  robot equipoAzul
  variables
    id: numero
    p: numero
    ca: numero
    
  comenzar
    
    
    RecibirMensaje (id, rf) {recibo si soy 3 o 4}
    derecha
    ca:= PosCa
    p:= 0
    
    juntarPapeles (p)
    BloquearEsquina (2,ca)
    mover
    juntarPapeles (p)
    BloquearEsquina (3,ca)
    mover
    LiberarEsquina (2,ca)
    juntarPapeles (p)
    mover
    LiberarEsquina (3,ca)
      
    repetir 96
      juntarPapeles (p)
      mover
    juntarPapeles (p)
    EnviarMensaje (id, rf)
    EnviarMensaje (p, rf)
       
  fin
  
  robot fiscalizador
  variables
  
    id: numero
    totF: numero
    totP: numero
    f, p: numero
    
  comenzar
    
    totF:= 0
    totP:= 0
    
    mandarIds {los identifico}
    
    repetir 4
    
      RecibirMensaje (id, *)
      si (id = 1)
        RecibirMensaje (f,r1)
        totF:= totF + f
   
      sino
        si (id = 2)
          RecibirMensaje (f,r2)
          totF:= totF + f
      
        sino 
      
          si (id = 3)
            RecibirMensaje (p,a1)
            totP:= totP + p
          
          sino
            RecibirMensaje (p,a2)
            totP:= totP + p
    
    si (totF > totP)
      Informar("Ganador equipo rojo", totF)
      
    sino 
      si (totF = totP)
        Informar ("Empate", totP,totF)
      
      sino 
        Informar ("Ganador equipo azul", totP)
    
    
  fin
  
  
variables
  
  r1, r2: equipoRojo
  a1, a2: equipoAzul
  rf: fiscalizador
  
comenzar

  AsignarArea (r1, areaR1tramo1)
  AsignarArea (r1, areaR1tramo2)
  AsignarArea (r1, esq1)
  AsignarArea (r1, esq2)
  
  AsignarArea (r2, areaR2tramo1)
  AsignarArea (r2, areaR2tramo2)
  AsignarArea (r2, esq3)
  AsignarArea (r2, esq4)
  
  AsignarArea (a1, areaA1tramo1)
  AsignarArea (a1, areaA1tramo2)
  AsignarArea (a1, esq1)
  AsignarArea (a1, esq3)
  
  AsignarArea (a2, areaA2tramo1)
  AsignarArea (a2, areaA2tramo2)
  AsignarArea (a2, esq2)
  AsignarArea (a2, esq4)
  
  AsignarArea (rf, areaJefe)
  
  Iniciar (rf, 1,1)
  Iniciar (r1, 2,1)
  Iniciar (r2, 3,1)
  Iniciar (a1, 1,98)
  Iniciar (a2, 1,99)
  
fin
