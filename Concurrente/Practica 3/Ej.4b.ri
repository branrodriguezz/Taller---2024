{Realice un programa en el que 4 robots mueven de a una todas las flores de la esquina (10,10) a la esquina (11,11).}
{ Para ello, cada robot que toma una flor de la esquina (10,10) la deposita en la esquina (11,11) y luego retorna a su esquina inicial.}
{Cada robot que finaliza (o, sea, que detecta que la esquina (10,10) se ha vaciado) deberá avisar al robot coordinador que ha finalizado. Cuando todos los robots finalizaron, el robot coordinador deberá informar qué robot finalizó último y a continuación deberá recolectar todas las flores de la esquina (11,11).}
{El robot coordinador inicia en la esquina (1,1).}
{Los robots inician en las esquinas (9,9) (9,10) (9,11) y (9,12)}
{Implemente una variante en la cual los robots luego de tomar cada flor de la esquina (10,10) vuelvan a su esquina inicial, pasen a la esquina (11,11) a depositarla y finalmente vuelvan a la esquina inicial.}

programa p3ej4
procesos
  proceso juntarFlores
  comenzar
    mientras (HayFlorEnLaEsquina)
      tomarFlor
  fin
  
  proceso mandarIds 
  comenzar
  
    EnviarMensaje (1, r1)
    EnviarMensaje (2, r2)
    EnviarMensaje (3, r3)
    EnviarMensaje (4, r4)
    
  fin
  
  proceso moverFlor (E avIni: numero; E caIni: numero)
  comenzar
    tomarFlor
    Pos (avIni, caIni)
    LiberarEsquina (10,10)
    BloquearEsquina (11,11)
    Pos (11,11)
    depositarFlor
    Pos (avIni,caIni)
    LiberarEsquina (11,11)
  fin
  
  proceso trasladarFlores (ES hayFlor: boolean)
  variables
    avIni, caIni: numero
  comenzar
    avIni:= PosAv
    caIni:= PosCa
    
    BloquearEsquina (10,10)
    Pos (10,10)
    si (HayFlorEnLaEsquina)
      moverFlor (avIni, caIni)
    sino
      hayFlor:= F
      Pos (avIni, caIni)
      LiberarEsquina (10,10)
      
  fin
  
  proceso recolectarFlores
  comenzar
    Pos (11,11)
    juntarFlores 
    Pos (1,1)
  fin
  
areas

  areaCoordinador: AreaP (1,1,1,1)
  areaInicial: AreaPC (9,9,9,12)
  areaEsq1: AreaP (10,10,10,10)
  areaEsq2: AreaP (11,11,11,11)
  
robots
  robot coordinador
  variables
    id: numero
  comenzar
    mandarIds
    repetir 4
      RecibirMensaje (id, *)
    Informar ('El ultimo fue r', id)
    recolectarFlores 
  fin
  
  robot trabajador
  variables   
    id: numero
    hayFlor: boolean
  comenzar
    hayFlor:= V
    RecibirMensaje (id, rc)
    mientras hayFlor
      trasladarFlores (hayFlor)
    EnviarMensaje (id, rc)
  fin
  
variables
  rc: coordinador
  r1, r2, r3, r4: trabajador 
comenzar

  AsignarArea (rc, areaCoordinador)
  AsignarArea (rc, areaEsq2)
  AsignarArea (r1, areaInicial)
  AsignarArea (r1, areaEsq1)
  AsignarArea (r1, areaEsq2)
  AsignarArea (r2, areaInicial)
  AsignarArea (r2, areaEsq1)
  AsignarArea (r2, areaEsq2)
  AsignarArea (r3, areaInicial)
  AsignarArea (r3, areaEsq1)
  AsignarArea (r3, areaEsq2)
  AsignarArea (r4, areaInicial)
  AsignarArea (r4, areaEsq1)
  AsignarArea (r4, areaEsq2)
  Iniciar (rc, 1,1)
  Iniciar (r1, 9,9)
  Iniciar (r2, 9,10)
  Iniciar (r3, 9,11)
  Iniciar (r4, 9,12)
  
fin
